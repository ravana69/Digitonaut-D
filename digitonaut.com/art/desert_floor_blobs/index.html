<!DOCTYPE html>
<html lang="en" >

<!-- Mirrored from digitonaut.com/art/desert_floor_blobs/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 08:07:14 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Desert Floor Blobs</title>

<link rel="stylesheet" href="style.css">

</head>
<body style='margin:0px;background:#000;color:#FFF;overflow:hidden;'>

<script id="fragmentShader" type="x-shader/x-fragment">
  uniform vec2 iResolution;
  uniform float iTime;
  uniform vec2 iMouse;
  
  #define PI 3.14159265359
  #define TAU 6.28318530718
  
  #define SPHERES 15.

  // https://iquilezles.org/www/articles/smin/smin.htm
  float smin( float a, float b, float k ) {
      float h = clamp(0.5 + 0.5 * (b-a) / k, 0.0, 1.0);
      return mix(b, a, h) - k * h * (1.0 - h);
  }
  
  float sdSphere(in vec3 p, in vec3 spherePos, in float radius) {
    return length(p - spherePos) - radius;
  }
  
  float getY(in float t) {
    float y = mod(t * 0.05, 1.);
    return y * 8. - 2.;
  }
  
  float getSize(in float t, in float amount) {
    return (sin(t) * 0.5 + 0.5) * amount + 0.25;
  }
  
  float map(in vec3 p) {
    float ground = p.y;
    float dist = ground;
    for (float i = 0.; i < SPHERES; i++) {
      float iNormal = i / SPHERES;
      float iNormalInv = 1.0 - iNormal;
      float radius = iNormal * 0.5;
      float cycles = 1.;//sin(iTime * 0.1) * 2.;
      float angleOffset = iNormalInv * TAU * 1.5;
      float rotation = iTime * -0.1;
      float x = sin(iNormal * TAU * cycles + rotation + angleOffset);
      float yFlux = iNormal * TAU;
      float y = sin(iTime + yFlux) + .25;
      float z = cos(iNormal * TAU * cycles + rotation + angleOffset);
      float sphere = sdSphere(p, vec3(x, y, z), radius);
      dist = smin(sphere, dist, iNormalInv * 0.2 + 0.1);
    }
    return dist;
  }

  vec3 normal(in vec3 pos) {
    vec2 e = vec2(0.0001, 0.);
	  return normalize(vec3(map(pos+e.xyy) - map(pos-e.xyy),
                          map(pos+e.yxy) - map(pos-e.yxy),
                          map(pos+e.yyx) - map(pos-e.yyx)));
  }
  
  float castRay(in vec3 ro, in vec3 rd) {
    float t = 0.;
    for (int i = 0; i < 200; i++) {
		vec3 pos = ro + t * rd;
      float h = map(pos);  
	    t += h;
        if (h < 0.001) break;        
        if (h > 100.) break;
    }
    if (t > 100.) t = -1.;
    return t;
  }
  
  void main(void) {
            
    // Normalize your coordinates from 0 to 1
    // vec2 uv = gl_FragCoord.xy / iResolution.xy;
    
    // If you prefer to map from -0.5 to 0.5, use this instead
    vec2 uv = gl_FragCoord.xy/iResolution.xy - .5;
    vec2 p = uv;
    vec2 mousePos = iMouse / iResolution.xy - .5;
            
    // If you want to avoid stretching your image, use this
    p.x *= iResolution.x / iResolution.y;
    mousePos.x *= iResolution.x / iResolution.y;
    float distToMouse = 1.0 - length(mousePos - uv);
  
    float an = 10.0*iMouse.x / iResolution.x;//5.;
    float anY = iMouse.y / iResolution.y;//5.;
  
    vec3 ro = vec3(6.*sin(an), anY * 5., 6.*cos(an));
    vec3 target = vec3(0.0, 0.5, 0.0); 
    
    vec3 ww = normalize(target - ro);
    vec3 uu = normalize(cross(ww, vec3(0., 1., 0.)));
    vec3 vv = normalize(cross(uu, ww));
    
    vec3 rd = normalize(p.x*uu + p.y*vv + 1.5*ww);
    
    vec3 col = vec3(0.65, 0.95, 1.9) - rd.y * 0.5;
    
    
    float d = castRay(ro, rd);

    if (d > 0.) {
        vec3 pos = ro + d * rd;
        vec3 nor = normal(pos);
        
        vec3 mater = vec3(0.18);
        
        vec3 sun_dir = normalize(vec3(1., 0.8, -0.2));
        float sun_dif = clamp(dot(nor, sun_dir), 0., 1.);
        float sun_shad = step(castRay(pos + nor * 0.0015, sun_dir), 0.);
        float sky_dif = clamp(0.5 + 0.5 * dot(nor, vec3(0., 1., 0.)), 0., 1.);
        float bounce_dif = clamp(dot(nor, vec3(0., -1., 0)), 0., 1.);
        
        col = mater * vec3(3.0, 2.1, 1.5) * sun_dif * sun_shad;
        col += mater * vec3(0.3, 0.4, 0.8) * sky_dif;
        col += mater * vec3(0.8, 0.5, 0.4) * bounce_dif;
    }
    
    col = pow(col, vec3(0.4545));
    
    gl_FragColor = vec4(col ,1.0);
  }
</script>

<script id="vertexShader" type="x-shader/x-vertex">
  void main() {
    gl_Position = vec4(position, 1.0);
  }
</script>
<div id="webgl-container"></div>
<script src='three.min.js'></script>
<script  src="script.js"></script>

</body>

<!-- Mirrored from digitonaut.com/art/desert_floor_blobs/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 08:07:19 GMT -->
</html>
